################ Peatio

######## Output

output "opendax_peatio_external_ip" {
  value = "${google_compute_address.peatio.address}"
}
output "opendax_peatio_internal_ip" {
  value = "${google_compute_instance.peatio.network_interface[0].network_ip}"
}

######## Instance

resource "google_compute_instance" "peatio" {
  name         = "peatio-${random_id.opendax.hex}"
  machine_type = "n1-standard-2"
  zone         = var.zone
  allow_stopping_for_update = true
  lifecycle {
    ignore_changes = [attached_disk]
  }
  boot_disk {
    initialize_params {
      image = var.image
      type  = "pd-ssd"
      size  = 30
    }
  }
  network_interface {
    network = google_compute_network.opendax.name
    access_config {
      nat_ip = google_compute_address.peatio.address
    }
  }
  service_account {
    scopes = ["storage-ro"]
  }
  tags = ["allow-webhook"]
  metadata = {
    sshKeys = "deploy:${file(var.ssh_public_key)}"
  }
  # Cannot execute for deploy user.(sudo does not work.)
  #provisioner "remote-exec" {
  #  inline = [
  #    "sudo hostname peatio-gcp",
  #    "sudo cp -f /etc/hostname /etc/hostname.bak",
  #    "sudo echo 'peatio-gcp' > /etc/hostname",
  #  ]
  #  connection {
  #    host        = self.network_interface[0].access_config[0].nat_ip
  #    type        = "ssh"
  #    user        = "deploy"
  #    private_key = file(var.ssh_private_key)
  #  }
  #}
  provisioner "local-exec" {
    command = "rm -rf /tmp/upload && mkdir -p /tmp/upload && rsync -rv --copy-links --safe-links --exclude=terraform ../../../ /tmp/upload/"
  }
  provisioner "remote-exec" {
    inline = [
      "mkdir -p /home/deploy/opendax",
    ]
    connection {
      host        = self.network_interface[0].access_config[0].nat_ip
      type        = "ssh"
      user        = "deploy"
      private_key = file(var.ssh_private_key)
    }
  }
  provisioner "file" {
    source      = "/tmp/upload/"
    destination = "/home/deploy/opendax"
    connection {
      host        = self.network_interface[0].access_config[0].nat_ip
      type        = "ssh"
      user        = "deploy"
      private_key = file(var.ssh_private_key)
    }
  }
  provisioner "remote-exec" {
    script = "../../../bin/install_peatio.sh"
    connection {
      host        = self.network_interface[0].access_config[0].nat_ip
      type        = "ssh"
      user        = "deploy"
      private_key = file(var.ssh_private_key)
    }
  }
  provisioner "remote-exec" {
    script = "../../../bin/start_peatio.sh"
    connection {
      host        = self.network_interface[0].access_config[0].nat_ip
      type        = "ssh"
      user        = "deploy"
      private_key = file(var.ssh_private_key)
    }
  }
}

######## IP & Disks

resource "google_compute_address" "peatio" {
  name = "opendax-ip-${random_id.opendax.hex}"
}
resource "google_compute_disk" "peatio" {
  name = "peatio-docker-volumes-${random_id.opendax.hex}"
  type  = "pd-ssd"
  zone  = var.zone
  size  = 60
}
resource "google_compute_attached_disk" "peatio" {
  disk     = google_compute_disk.peatio.id
  instance = google_compute_instance.peatio.id
}

######## Cloudflare

#### Internal

resource "cloudflare_record" "internal-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "peatio.internal.gcp"
  value   = google_compute_instance.peatio.network_interface[0].network_ip
  type    = "A"
  ttl     = 1
}

#### External

resource "cloudflare_record" "peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "kibana-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "kibana.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "www-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "www.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "pma-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "pma.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "superset-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "superset.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "swagger-barong-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "swagger-barong.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
resource "cloudflare_record" "swagger-peatio" {
  zone_id = var.cloudflare_zone_id
  name    = "swagger.gcp"
  value   = google_compute_address.peatio.address
  type    = "A"
  ttl     = 1
}
